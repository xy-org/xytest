import xy.ctti;
import libxy.stdio;

struct TestError {
    msg: Str~MemManaged;
}

func to(te: TestError, :Bool) te.msg.size != 0;

*func unhandled(te: TestError) {
    print(te.msg);
}

*func assert(
    cond: Bool,
    msg: Str = "",
) if (!cond) fail(
    file=fileof(^cond), line=linenoof(^cond),
    src=srclineof(^cond), msg=msg,
);

*func equal(
    lhs: pseudo Any, rhs: pseudo Any,
    cmpRes : Bool = ^cmpEq(lhs, rhs),
) if (!cmpRes) {
    msg : Fstr;
    ^append(msg, lhs);
    append(msg, " != ");
    ^append(msg, rhs);
    fail(
        file=fileof(^lhs), line=linenoof(^lhs), src=srclineof(^lhs),
        msg=msg'to(Str)
    );
};

func fail(file: Str, line: Uint, src: Str, msg: Str) -> void | TestError {
    fullmsg := f"""
        \n{file}:{line} Assertion failed
        > {src}
        {msg}
    ";
    error TestError{ fullmsg => };
}
