import xy.ctti;
import libxy.stdio;

struct TestError {
    msg: String,
}

func to(te: TestError, :Bool) te.msg'isEmpty'not;

*func unhandled(te: TestError) {
    print(te.msg);
}

*func assert(
    cond: Bool,
    msg: String = "",
) if (!cond) fail(
    file=fileof(^cond), line=linenoof(^cond),
    src=srclineof(^cond), msg=msg,
);

*func equal(
    lhs: pseudo Any, rhs: pseudo Any,
    cmpRes : Bool = ^cmpEq(lhs, rhs),
) if (!cmpRes) {
    msg : Fstring;
    ^append(msg, lhs);
    append(msg, " != ");
    ^append(msg, rhs);
    fail(
        file=fileof(^lhs), line=linenoof(^lhs), src=srclineof(^lhs),
        msg=msg'to(String)
    );
};

func fail(file: String, line: Uint, src: String, msg: String) -> void | TestError {
    fullmsg := f"""
        \n{file}:{line} Assertion failed
        > {src}
        {msg}
    ";
    error TestError{ fullmsg => };
}
